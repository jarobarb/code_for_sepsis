# XPP\AUTO file

# Parameters as of 7/19/2018
par c1=0.5, k1=0.5, Bsource=128
par nu1=0.08, nu2=0.12, muM=0.05, a1=0.04, muA=0.05
par kM=0.01, kB=0.2, kepsilon=0.02, kAMEps=48, kBL=0.6
par sL=0.005, muL=0.002, kLB=0.01, sA=0.0125, tau=24
par kA=44.3872493024671, f=318.233194718556
par k5=0.0128450756926777, B_infy=1.98056838468433
par k_D=0.033101761350186, Bs=0.0117742436505237
# T is reserved for time, so using 'Th' for threshold
par Th=1

# Dosing function
# D(t)=k_D*Bsource*exp(-k_D*t)
D(t)=0

#Equations
b'=D(t)+k1*b*(1-(b/(B_infy/Bs)))-((k5*b*m)/(1+kA*a))-(kBL*sL*b)/(muL+kLB*b*Bs)
m'=(nu1*(kM*m+kB*c1*Bs*b+kepsilon*eps))/((nu2+kM*m+kB*c1*Bs*b+kepsilon*eps)*(1+kA*a))-muM*m
a'=sA+(a1*(m+kAMEps*eps)/((1+m+kAMEps*eps)*(1+kA*a)))-muA*a
eps'=((-eps)/tau)+max(f*m-Th,0)/(1+kA*a)

#Initial Conditions
init b=Bsource
init m=0.0
init a=0.25
init eps=0.0

#Numerical Specifications
@ method=rk, tol=0.0001, dt=0.1, total=1000.0, bounds=10000000
@ xlo=0, xhi=1000, ylo=0, yhi=10, bell=0
@ maxstore=100000
done